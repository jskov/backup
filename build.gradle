plugins {
    id 'eclipse'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

def declaredVersion = System.getenv("BUILD_SOURCEBRANCH")
if (declaredVersion != null && declaredVersion.startsWith("refs/tags/")) {
	rootProject.version = declaredVersion.substring(10)
} else {
	version = "1-SNAPSHOT"
}
println "Builds version $version"


group = "dk.mada.backup"

repositories {
     mavenCentral()
}

dependencies {
	implementation 'org.slf4j:slf4j-api:1.8.0-beta4'
	implementation 'ch.qos.logback:logback-classic:1.3.0-alpha4'
	implementation 'org.apache.commons:commons-compress:1.18'
	implementation "com.beust:jcommander:1.72"

    testCompile "org.assertj:assertj-core:3.11.1"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
}

task makeAzureDockerImage() {
	doFirst {
		copy {
			into "build/azure-docker"
			from "src/azure"
			from ("src/test/data/gpghome") {
				include "*.asc"
			}
		}
	 	project.exec {
			commandLine("docker", "build", "-t", "azure-backup", "build/azure-docker")
		}
	}
}

task prepareAzureGpgTestHome() {
	doFirst {
		project.mkdir("build/azure-gpg-home")
	 	project.exec {
			commandLine("gpg", "--homedir", "build/azure-gpg-home", "--import", "src/test/data/gpghome/exported-secret-key.asc")
		}
	 	project.exec {
			commandLine("gpg", "--homedir", "build/azure-gpg-home", "--import-ownertrust", "src/test/data/gpghome/exported-owner-trust.asc")
		}
	}
}

test {
	dependsOn(prepareAzureGpgTestHome)
    useJUnitPlatform {
        excludeTags "accept"
    }  
    ignoreFailures = true
}

task zipReports(type: Zip, dependsOn: "test") {
	archiveFileName = "test-reports.zip"
	destinationDirectory = file("build")
	from "build/reports"
	
	doLast {
		println "##vso[build.uploadlog]" + file("build").absolutePath + "/test-reports.zip"
		
		println "---LOG---"
		println file("build/test-results/test/TEST-unit.backup.NondestructionTest.xml").text
		println "---LOG---"
	}
}

task acceptTest(type: Test) {
	dependsOn(prepareAzureGpgTestHome)
    useJUnitPlatform {
  	    includeTags "accept"
    }
    ignoreFailures = true
}

acceptTest.shouldRunAfter("test")
tasks.check.dependsOn(acceptTest)

jar {
	manifest {
		attributes(
			'Git-Hash' : (System.getenv('BUILD_SOURCEVERSION') ?: "no-version"),
			'Implementation-Version' : version,
			'Main-Class' : "dk.mada.backup.cli.CliMain"
		)
	}
	archiveBaseName = "backup"
}

shadowJar {
	classifier = null
	destinationDir = file("dist")
}

task release(dependsOn: ["check", "shadowJar", "zipReports"]) {
	doLast {
		println "Release $version"
	}
}

sourceCompatibility = 11
targetCompatibility = 11

tasks.clean { delete("dist") }

tasks.eclipseClasspath.dependsOn("cleanEclipseClasspath")
