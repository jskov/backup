plugins {
    id 'checkstyle'
    id 'eclipse'
    id 'jacoco'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.gradle.crypto.checksum' version '1.1.0'
    id 'org.sonarqube' version '3.4.0.2513'
}

group "${groupId}"

import org.gradle.crypto.checksum.Checksum

ext {
	successMarker = project.file("build/all-tests-passed")
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.slf4j:slf4j-jdk14:1.8.0-beta4'
	implementation 'org.apache.commons:commons-compress:1.21'
	implementation "com.beust:jcommander:1.82"

    testImplementation  platform("org.junit:junit-bom:$junitVersion")
    testImplementation  'org.junit.jupiter:junit-jupiter'
    testImplementation  'org.junit.jupiter:junit-jupiter-params'
    testImplementation  'org.junit.jupiter:junit-jupiter-engine'
    testImplementation  'org.junit.jupiter:junit-jupiter-api'
    testImplementation  'org.junit.platform:junit-platform-runner'
    testImplementation  "org.assertj:assertj-core:$assertJVersion"
}

task makeActionsDockerImage() {
	doFirst {
		copy {
			into "build/actions-docker"
			from "src/actions"
			from ("src/test/data/gpghome") {
				include "*.asc"
			}
		}
	 	project.exec {
			commandLine("podman", "build", "-t", "actions-backup", "build/actions-docker")
		}
	}
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

task prepareActionsGpgTestHome() {
	doFirst {
		project.mkdir("build/actions-gpg-home")
	 	project.exec {
			commandLine("gpg", "--homedir", "build/actions-gpg-home", "--import", "src/test/data/gpghome/exported-secret-key.asc")
		}
	 	project.exec {
			commandLine("gpg", "--homedir", "build/actions-gpg-home", "--import-ownertrust", "src/test/data/gpghome/exported-owner-trust.asc")
		}
	}
}

task makeSuccessMarker() {
	doFirst {
		project.mkdir("build")
		successMarker.createNewFile()
	}
}

task acceptTest(type: Test) {
	dependsOn(makeSuccessMarker)
	dependsOn(prepareActionsGpgTestHome)
    useJUnitPlatform {
  	    includeTags "accept"
    }
    ignoreFailures = true
    forkEvery = 1

    afterTest { td, tr ->
		if (tr.resultType == org.gradle.api.tasks.testing.TestResult.ResultType.FAILURE) {
			successMarker.delete()
		}
    }
}

test {
	dependsOn(makeSuccessMarker)
	dependsOn(prepareActionsGpgTestHome)
    useJUnitPlatform {
        excludeTags "accept"
    }  
    ignoreFailures = true

    afterTest { td, tr ->
		if (tr.resultType == org.gradle.api.tasks.testing.TestResult.ResultType.FAILURE) {
			successMarker.delete()
		}
    }
}

acceptTest.shouldRunAfter("test")
tasks.check.dependsOn(acceptTest)

jacocoTestReport {
    reports {
        xml.required.set(true)
    }
    dependsOn test // tests are required to run before generating the report
}

checkstyle {
    ignoreFailures = true
}

sonarqube {
    properties {
        property "sonar.inclusions", "src/main/**"
        property "sonar.java.checkstyle.reportPaths", "build/reports/checkstyle/main.xml"

        property "sonar.projectKey", "jskov_backup"
        property "sonar.organization", "jskov-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jar {
	manifest {
		attributes(
			'Git-Hash' : (System.getenv('BUILD_SOURCEVERSION') ?: "no-version"),
			'Implementation-Version' : project.version,
			'Main-Class' : "dk.mada.backup.cli.CliMain"
		)
	}
	archiveBaseName = "backup"
}

shadowJar {
	classifier = null
	archiveFileName = "backup-shadow.jar"
}

tasks.clean { delete("dist") }

tasks.eclipseClasspath.dependsOn("cleanEclipseClasspath")


task buildNative(type: Exec, dependsOn: jar) {
	doFirst {
		project.delete("build/backup")
	}
	commandLine 'native-image', '-cp', sourceSets.main.getRuntimeClasspath().getAsPath(), "dk.mada.backup.cli.CliMain", "build/backup"
}
